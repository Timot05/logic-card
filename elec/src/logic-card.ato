import CPG151101S03 from "CPG151101S03.ato"
import RP2040Kit from "rp2040/RP2040Kit.ato"
import USBCConn from "usb-connectors/usb-connectors.ato"
import LDOReg3V3 from "ldo_reg.ato"
import Resistor from "generics/resistors.ato"
import Power from "generics/interfaces.ato"
# battery holder
import BH_AAA_A1AJ024 from "BH-AAA-A1AJ024.ato"
import Battery from "batteries/batteries.ato"
import BatteryCR2032 from "batteries/batteries.ato"
# switch
import B3F_4005_C397283 from "B3F-4005_C397283.ato"
# mosfet
import _2N7002KW_R1_00001 from "2N7002KW_R1_00001.ato"

import ButtonTS1187A from "button.ato"
# Diode
import _1N4148W_T4 from "1N4148WT4.ato"

# import pogo pin
import KH_102540_DZ from "pogo_pin_pad.ato"
import PogoPad from "pogo_pin_pad.ato"

module SwitchPullUp:
    power = new Power
    signal out
    switch = new B3F_4005_C397283
    r_pd = new Resistor
    r_pd.value = 10kohms +/- 5%
    r_pd.footprint = "R0603"

    power.vcc ~ r_pd.p1; r_pd.p2 ~ out
    out ~ switch.in; switch.out ~ power.gnd

component RGBLed:
    # https://www.adafruit.com/product/848
    signal b ~ pin 1
    signal g ~ pin 2
    signal vcc ~ pin 3
    signal r ~ pin 4

    # Pitch is 1.25. Hoping this 1.27 will work
    footprint = "Connector_PinHeader_1.27mm:PinHeader_1x04_P1.27mm_Vertical"

    mpn = "dnp"

module RGBLedKit:
    signal r_sink
    signal g_sink
    signal vcc
    signal b_sink

    rr = new Resistor
    rg = new Resistor
    rb = new Resistor

    led = new RGBLed

    led.vcc ~ vcc
    led.r ~ rr.p1; rr.p2 ~ r_sink
    led.g ~ rg.p1; rg.p2 ~ g_sink
    led.b ~ rb.p1; rb.p2 ~ b_sink

    rg.value = 100ohms +/- 10%
    rb.value = 100ohms +/- 10%
    rr.value = 1.5kohms +/- 10%

    rg.footprint = "R0603"
    rb.footprint = "R0603"
    rr.footprint = "R0603"



module ButtonPullup:
    btn = new ButtonTS1187A

    signal output
    power = new Power

    pullup = new Resistor
    pullup.value = 10kohms +/- 10%
    pullup.footprint = "R0402"

    power.vcc ~ pullup.p1; pullup.p2 ~ btn.p1; btn.p2 ~ power.gnd
    output ~ pullup.p2

module CoinCells3S1P from Battery:
    cell_1 = new BatteryCR2032
    cell_2 = new BatteryCR2032
    cell_3 = new BatteryCR2032

    power.vcc  ~ cell_1.power.vcc
    cell_1.power.gnd ~ cell_2.power.vcc
    cell_2.power.gnd ~ cell_3.power.vcc
    cell_3.power.gnd ~ power.gnd


module LogicCard:
    # Power stuff
    ldo = new LDOReg3V3
    aaa = new CoinCells3S1P

    aaa.power.gnd ~ ldo.power_in.gnd

    aaa_diode = new _1N4148W_T4
    aaa.power.vcc ~ aaa_diode.A; aaa_diode.K ~ ldo.power_in.vcc

    # A micro
    micro = new RP2040Kit
    usb_c = new USBCConn
    usb_diode = new _1N4148W_T4
    usb_c.power.vcc ~ usb_diode.A; usb_diode.K ~ ldo.power_in.vcc
    usb_c.power.gnd ~ ldo.power_in.gnd

    usb_c.usb2 ~ micro.usb_if

    micro.reset_btn -> ButtonPullup
    micro.boot_btn -> ButtonPullup

    ldo.power_out ~ micro.power

    # Two switches for the gate
    switch_1 = new SwitchPullUp
    switch_2 = new SwitchPullUp

    switch_1.out ~ micro.micro.gpio0
    switch_2.out ~ micro.micro.gpio14

    switch_1.power ~ ldo.power_out
    switch_2.power ~ ldo.power_out

    pogo_1_program = new KH_102540_DZ
    pogo_2_program = new KH_102540_DZ
    pogo_3_program = new KH_102540_DZ
    # Connecting the micro to the plates
    micro.micro.gpio1 ~ pogo_1_program.in
    micro.micro.gpio2 ~ pogo_2_program.in
    micro.micro.gpio3 ~ pogo_3_program.in

    r_pad_1 = new Resistor
    r_pad_1.footprint = "R0402"
    r_pad_1.value = 10kohms +/- 5%
    pogo_1_program.in ~ r_pad_1.p2; r_pad_1.p1 ~ ldo.power_out.gnd

    r_pad_2 = new Resistor
    r_pad_2.footprint = "R0402"
    r_pad_2.value = 10kohms +/- 5%
    pogo_2_program.in ~ r_pad_2.p2; r_pad_2.p1 ~ ldo.power_out.gnd

    r_pad_3 = new Resistor
    r_pad_3.footprint = "R0402"
    r_pad_3.value = 10kohms +/- 5%
    pogo_3_program.in ~ r_pad_3.p2; r_pad_3.p1 ~ ldo.power_out.gnd

    # Lastly, an LED
    led_kit = new RGBLedKit
    r_mosfet = new _2N7002KW_R1_00001
    g_mosfet = new _2N7002KW_R1_00001
    b_mosfet = new _2N7002KW_R1_00001

    led_kit.r_sink ~ r_mosfet.drain
    led_kit.g_sink ~ g_mosfet.drain
    led_kit.b_sink ~ b_mosfet.drain

    led_kit.vcc ~ ldo.power_out.vcc

    micro.micro.gpio24 ~ r_mosfet.gate
    micro.micro.gpio23 ~ g_mosfet.gate
    micro.micro.gpio22 ~ b_mosfet.gate

    r_mosfet.source ~ ldo.power_out.gnd
    g_mosfet.source ~ ldo.power_out.gnd
    b_mosfet.source ~ ldo.power_out.gnd

module ProgramCard:
    pogo_pad_1_program = new PogoPad
    pogo_pad_2_program = new PogoPad
    pogo_pad_3_program = new PogoPad

    pogo_pad_3v3 = new PogoPad

module CardAnd from ProgramCard:
    pogo_pad_3v3.in ~ pogo_pad_3_program.in

module CardOr from ProgramCard:
    pogo_pad_3v3.in ~ pogo_pad_2_program.in

module CardWire from ProgramCard:
    pogo_pad_3v3.in ~ pogo_pad_1_program.in
